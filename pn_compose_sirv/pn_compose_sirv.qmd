---
title: Adding vaccination to an SIR model using AlgebraicPetri.jl
date: 2024-08-06
author: Simon Frost (@sdwfrost)
format:
    html: default
    docx: default
    gfm: default
    pdf: default
---

## Introduction

This notebook demonstrates how to add a new transition between states to an existing model; in this case, adding vaccination to an SIR model.

## Libraries

```{julia}
using AlgebraicPetri,AlgebraicPetri.TypedPetri
using Catlab, Catlab.CategoricalAlgebra, Catlab.Programs
using Catlab.WiringDiagrams, Catlab.Graphics
using AlgebraicDynamics.UWDDynam
using LabelledArrays
using OrdinaryDiffEq
using Plots
```

## Transitions

We first define a labelled Petri net that has the different types of transition in our models. The first argument is an array of state names as symbols (here, a generic `:Pop`), followed by the transitions in the model. Transitions are given as `transition_name=>((input_states)=>(output_states))`.

```{julia}
epi_lpn = LabelledPetriNet(
  [:Pop],
  :infection=>((:Pop, :Pop)=>(:Pop, :Pop)),
  :recovery=>(:Pop=>:Pop),
  :vaccination=>(:Pop=>:Pop)
);
```

Labelled Petri nets contain four types of fields; `S`, states or species; `T`, transitions; `I`, inputs; and `O`, outputs.

Next, we define the transmission model as an undirected wiring diagram using the `@relation` macro, referring to the transitions in our labelled Petri net above (`infection` and `recovery`). We include a reference to `Pop` in the definition of the state variables to allow us to do this.

```{julia}
sir_uwd = @relation (S, I, R) where (S::Pop, I::Pop, R::Pop) begin
    infection(S, I, I, I)
    recovery(I, R)
end;
```

We then use `oapply_typed`, which takes in a labelled Petri net (here, `epi_lpn`) and an undirected wiring diagram (`si_uwd`), where each of the boxes is labeled by a symbol that matches the label of a transition in the Petri net, in addition to an array of symbols for each of the rates in the wiring diagram. This produces a Petri net given by colimiting the transitions together, and returns the `ACSetTransformation` from that Petri net to the type system.

```{julia}
sir_acst = oapply_typed(epi_lpn, sir_uwd, [:β, :γ]);
```

To obtain the labelled Petri net, we extract the domain of the `ACSetTransformation` using `dom`.

```{julia}
sir_lpn = dom(sir_acst);
```

We can obtain a GraphViz representation of the labelled Petri net using `to_graphviz`.

```{julia}
to_graphviz(sir_lpn)
```

We now define another model that considers transitions between `S` and `R` due to vaccination (at rate σ).

```{julia}
v_uwd = @relation (S, R) where (S::Pop, R::Pop) begin
    vaccination(S, R)
end
v_acst = oapply_typed(epi_lpn, v_uwd, [:σ])
v_lpn = dom(v_acst)
to_graphviz(v_lpn)
```

To glue the SI and vaccination models together to make an SIR model, we first define an undirected wiring diagram which contains all our states, and two transitions.

```{julia}
sirv_uwd = @relation (S, I, R) where (S::Pop, I::Pop, R::Pop) begin
    sir(S, I, R)
    v(S, R)
end;
```

We then create a `StructuredMulticospan` using this wiring diagram, telling `oapply` that `si` in the wiring diagram corresponds to the `si_lpn` labelled Petri net, etc.. `Open` converts a PetriNet to an OpenPetriNet where each state is exposed as a leg of the cospan, allowing it to be composed over an undirected wiring diagram.

```{julia}
sirv_smc = oapply(sirv_uwd, Dict(
    :sir => Open(sir_lpn),
    :v => Open(v_lpn),
));
```

We extract the labelled Petri net by extracting the object that is the codomain of all the legs, using the `apex` function.

```{julia}
sirv_lpn = apex(sirv_smc)
to_graphviz(sirv_lpn)
```

## Running the model

To run an ODE model from the labelled Petri net, we generate a function that can be passed to SciML's `ODEProblem` using  `vectorfield`.

```{julia}
sirv_vf = vectorfield(sirv_lpn);
```

The initial conditions and parameter values are written as labelled arrays.

```{julia}
u0 = @LArray [990.0, 10.0, 0.0] Tuple(snames(sirv_lpn))
```

```{julia}
p = @LArray [0.5/1000, 0.25, 0.05] Tuple(tnames(sirv_lpn))
```

```{julia}
tspan = (0.0, 40.0);
```

We can now use the initial conditions, the time span, and the parameter values to simulate the system.

```{julia}
sirv_prob = ODEProblem(sirv_vf, u0, tspan, p)
sirv_sol = solve(sirv_prob, Rosenbrock32())
plot(sirv_sol)
```
